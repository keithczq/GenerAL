.PROGRAM a.ros()
;
; ABSTRACT:  Main entry point for the ROS - Staubli/Adept serial interface.  
;
; INPUTS:       None.
;
; OUTPUTS:      None.
;
; SIDE EFFECTS: Starts the serial command server and the serial feedback server.
;
; DATA STRUCT:  None.
;
;
; Define globals

        delay.cmd = 1/10    ;Loop delay for joint commands
        delay.fdb = 1/10    ;Loop delay for joint feedback

; Start running the Serial protocols

        run = TRUE
        EXECUTE 5 ros.srv.cmd()
        EXECUTE 6 ros.srv.fdb()

; Return

        RETURN
.END
.PROGRAM ros.msg.create(msg.len, msg.type, cmd.type, msg.reply, msg.unused, $msg)
;
; ABSTRACT:  Format a ROS message.
;
; INPUTS:       msg.len     The length of the message in bytes
;               msg.type    The message type
;               cmd.type    The command type
;               msg.reply   The message reply
;               msg.unused  The unused portion of the message (0)
;
; OUTPUTS:      $msg        The formatted message
;
; SIDE EFFECTS: None.
;
; DATA STRUCT:  None.
;
;
        AUTO $msg.len, $msg.type, $cmd.type, $msg.reply, $msg.unused

        ; Convert inputs to bytes

        $msg.len = $LNGB(msg.len)
        $msg.type = $LNGB(msg.type)
        $cmd.type = $LNGB(cmd.type)
        $msg.reply = $LNGB(msg.reply)
        $msg.unused = $LNGB(msg.unused)

        ; Reverse the bytes to match ROS

        CALL ros.rev.bytes($msg.len, $msg.len.rev)
        CALL ros.rev.bytes($msg.type, $msg.type.rev)
        CALL ros.rev.bytes($cmd.type, $cmd.type.rev)
        CALL ros.rev.bytes($msg.reply, $msg.reply.rev)
        CALL ros.rev.bytes($msg.unused, $msg.unused.rev)

        ; Format the message

        $msg = ""
        $msg = $ENCODE($msg.len.rev,$msg.type.rev,$cmd.type.rev,$msg.reply.rev,$msg.unused.rev)

        RETURN

.END
.PROGRAM ros.read.4bytes(slun, long)
;
; ABSTRACT:  Reads 4 bytes from the ROS Serial port and converts it to a V+ string.
;
; INPUTS:       slun     The Serial slun to read
;
; OUTPUTS:      $msg    The V+ string
;
; SIDE EFFECTS: Reverses the incoming byte order.
;
; DATA STRUCT:  None.
;

        AUTO $tmp, i, c[4]

        $tmp = ""
        FOR i = 1 TO 4
             c[i] = GETC(slun,2)
            $tmp = $tmp+$CHR(c[i])
        END
        CALL ros.rev.bytes($tmp, $msg)
        long = LNGB($msg)

.END
.PROGRAM ros.read.joints(slun, jts[])
;
; ABSTRACT: Reads 10 joint angles from ROS. 
;
; INPUTS:       slun     The Serial slun to read
;
; OUTPUTS:      jts[10] The array of joint angles in degrees
;
; SIDE EFFECTS: Reverses the byte order 
;
; DATA STRUCT:  None


        AUTO c[4], idx, $tmp, $msg.jts[10]

        FOR idx = 1 TO 10
            $tmp = ""
            FOR i = 1 TO 4
                c[i] = GETC(lun,2)
                $tmp = $tmp+$CHR(c[i])
            END
            CALL ros.rev.bytes($tmp, $msg.jts[idx])
            jts[idx] = FLTB($msg.jts[idx])
            jts[idx] = (jts[idx]*180)/PI
        END

.END
.PROGRAM ros.rev.bytes($in, $out)
;
; ABSTRACT: Reverse the bytes  
;
; INPUTS:       $in     The input string
;
; OUTPUTS:      $out    The output string
;
; SIDE EFFECTS: Reverses the bytes
;
; DATA STRUCT:  None.


        AUTO str.len, i, $chr

        str.len = LEN($in)
        $chr = ""
        $out = ""

        FOR i = str.len TO 1 STEP -1
            $chr = $MID($in,i,1)
            $out = $out+$chr
        END

        RETURN
.END
.PROGRAM ros.srv.cmd()
;
; ABSTRACT:  Serial server for listening to joint commands from ROS.
;
; INPUTS:       None.
;
; OUTPUTS:      None.
;
; SIDE EFFECTS: Causes robot motion.
;
; DATA STRUCT:  None.

        AUTO slun, status, idx
        AUTO msg.len, msg.type, cmd.type, msg.reply, msg.unused, $msg.jts[10], $tmp.str
        AUTO REAL jts[10]
        AUTO $msg

        ; Initialize autos

        msg.len = 0
        msg.type = 0
        cmd.type = 0
        msg.reply = 0

        ; Initialize joints

        FOR idx = 1 TO 10
            jts[idx] = 0
        END

        ; Open a serial port

        ATTACH (slun, 11) "SERIAL:1"
        status = IOSTAT(slun)
        IF (status < 0) THEN
            TYPE "Error opening cmd serial port: "+$ERROR(status)
            GOTO 100
        END

        ; Read 4 bytes from connection

        CALL ros.read.4bytes(slun, msg.unused)
        TYPE "ROS connected to V+ command server"

        ; Attach the robot and set speeds and accels for move

        SELECT ROBOT = 1
        ATTACH ()
        SPEED 20 ALWAYS
        ACCEL (1) 100, 100

        ; Server loop 

        WHILE run DO

            ; Read data from ROS

            CALL ros.read.4bytes(slun, msg.len)     ;Message length
            CALL ros.read.4bytes(slun, msg.type)    ;Message type
            CALL ros.read.4bytes(slun, cmd.type)    ;Command type
            CALL ros.read.4bytes(slun, msg.reply)   ;Message reply
            CALL ros.read.4bytes(slun, msg.unused)  ;Discard 4 bytes
            CALL ros.read.joints(slun, jts[])       ;Joint values

            ; Check for STOP from ROS

            IF (msg.unused == -2) THEN
                GOTO 100
            END

            ; Type the joint values for debug

            ;TYPE $ENCODE(jts[1],",",jts[2],",",jts[3],",",jts[4],",",jts[5],",",jts[6])

            ; Format a reply message to send to ROS

            CALL ros.msg.create(56, 10, 3, 1, 0, $msg)

            ; Append the joint position to the message

            SET #loc = #PPOINT(jts[1],jts[2],jts[3],jts[4],jts[5],jts[6])
            FOR idx = 1 TO 10
                jts[idx] = 0
                $msg = $msg+$msg.jts[idx]
            END

            ; Send the message to ROS

            WRITE (slun) $msg, /S

            ; Start the move

            MOVE #loc

            ; Loop delay

            ;WAIT.EVENT , delay.cmd

        END

        ; Close the Serialport

        DETACH (slun)

        ; Return

        CALL ros.stop()

   100  RETURN

.END
.PROGRAM ros.srv.fdb()
;
; ABSTRACT:  Serial server for sending joint feedback to ROS.  
;
; INPUTS:       None.
;
; OUTPUTS:      None.
;
; SIDE EFFECTS: None.
;
; DATA STRUCT:  Server streams  <LENGTH(bytes), <10>, <1(COMM_TYPE - TOPIC)>, <0 (REPLY_TYPE - N/A)>,
;                               <UNUSED <JOINT_DATA[10] (in rads (floats))>
;


        AUTO idx, slun, status
        AUTO REAL jts[10]
        AUTO $tmp.str, $msg.len, $msg.type, $cmd.type, $msg.reply, $msg.jts[10], $msg.unused
        AUTO LOC #cur.loc
        AUTO $msg

        $tmp.str = ""

        ; Initialize joints

        FOR idx = 1 TO 10
            jts[idx] = 0
        END

        ; Open a Serial port

        ATTACH (slun, 4) "SERIAL:1"
        status = IOSTAT(slun)
        IF (status < 0) THEN
            TYPE "Error opening Serial port: "+$ERROR(status)
            GOTO 100
        END

        ; Server loop 

        WHILE run DO

            ; Format a message to send to ROS

            CALL ros.msg.create(56, 10, 1, 0, 0, $msg)

            ; Get the current joint position

            HERE #cur.loc
            DECOMPOSE jts[1] = #cur.loc

            ; Format the joint data and append it to the message

            FOR idx = 1 TO 10
                jts[idx] = ((jts[idx]*PI)/180)
                $tmp.str = $FLTB(jts[idx])
                CALL ros.rev.bytes($tmp.str, $msg.jts[idx])
                $msg = $msg+$msg.jts[idx]
            END

            ; Send the message to ROS

            WRITE (slun) $msg, /S

            ; Loop delay

            WAIT.EVENT , delay.fdb

        END

        ; Close the Serial port

        DETACH (slun)

        ; Return

   100  RETURN

.END
.PROGRAM ros.stop()
;
; ABSTRACT:  Stops the V+ servers.
;
; INPUTS:       None.
;
; OUTPUTS:      None.
;
; SIDE EFFECTS: Stops ros.srv.cmd() and ros.srv.fdb()
;
; DATA STRUCT:  None.
;

        run = FALSE

.END
